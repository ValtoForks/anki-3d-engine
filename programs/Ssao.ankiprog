<!-- 
Copyright (C) 2009-2018, Panagiotis Christopoulos Charitos and contributors.
All rights reserved.
Code licensed under the BSD License.
http://www.anki3d.org/LICENSE
-->
<shaderProgram>
	<mutators>
		<mutator name="USE_NORMAL" values="0 1"/>
	</mutators>

	<shaders>
		<shader type="vert">
			<source><![CDATA[
#include "shaders/QuadVert.glsl"
			]]></source>
		</shader>

		<shader type="frag">
			<inputs>
				<input name="NOISE_MAP_SIZE" type="uint" const="1"/>
				<input name="FB_SIZE" type="uvec2" const="1"/>
				<input name="RADIUS" type="float" const="1"/>
				<input name="BIAS" type="float" const="1"/>
				<input name="STRENGTH" type="float" const="1"/>
				<input name="SAMPLE_COUNT" type="uint" const="1"/>
			</inputs>

			<source><![CDATA[
#include "shaders/Common.glsl"
#include "shaders/Pack.glsl"
#include "shaders/Functions.glsl"

layout(location = 0) in vec2 in_uv;

layout(location = 0) out float out_color;

layout(ANKI_UBO_BINDING(0, 0), std140, row_major) uniform _blk
{
	vec4 u_unprojectionParams;
	vec4 u_projectionMat;
	mat3 u_viewRotMat;
};

layout(ANKI_TEX_BINDING(0, 0)) uniform sampler2D u_mMsDepthRt;
layout(ANKI_TEX_BINDING(0, 1)) uniform sampler2DArray u_noiseMap;
#if USE_NORMAL
layout(ANKI_TEX_BINDING(0, 2)) uniform sampler2D u_msRt;
#endif

#if USE_NORMAL
// Get normal
vec3 readNormal(in vec2 uv)
{
	vec3 normal;
	readNormalFromGBuffer(u_msRt, uv, normal);
	normal = u_viewRotMat * normal;
	return normal;
}
#endif

// Read the noise tex
vec3 readRandom(vec2 uv, float layer)
{
	const vec2 tmp = vec2(float(FB_SIZE.x) / float(NOISE_MAP_SIZE), float(FB_SIZE.y) / float(NOISE_MAP_SIZE));
	vec3 r = texture(u_noiseMap, vec3(tmp * uv, layer)).rgb;
	return r;
}

// Returns the Z of the position in view space
float readZ(in vec2 uv)
{
	float depth = textureLod(u_mMsDepthRt, uv, 0.0).r;
	float z = u_unprojectionParams.z / (u_unprojectionParams.w + depth);
	return z;
}

// Read position in view space
vec3 readPosition(in vec2 uv)
{
	vec3 fragPosVspace;
	fragPosVspace.z = readZ(uv);
	fragPosVspace.xy = UV_TO_NDC(uv) * u_unprojectionParams.xy * fragPosVspace.z;

	return fragPosVspace;
}

vec4 project(vec4 point)
{
	return projectPerspective(point, u_projectionMat.x, u_projectionMat.y, u_projectionMat.z, u_projectionMat.w);
}

void main(void)
{
	vec2 ndc = UV_TO_NDC(in_uv);

	// Compute origin
	vec3 origin = readPosition(in_uv);

	// Get normal
#if USE_NORMAL
	vec3 normal = readNormal(in_uv);
#else
	vec3 normal = normalize(cross(dFdx(origin), dFdy(origin)));
#endif

	// Find the projected radius
	vec3 sphereLimit = origin + vec3(RADIUS, 0.0, 0.0);
	vec4 projSphereLimit = project(vec4(sphereLimit, 1.0));
	vec2 projSphereLimit2 = projSphereLimit.xy / projSphereLimit.w;
	float projRadius = length(projSphereLimit2 - ndc);

	// Loop to compute
	float ssao = 0.0;
	for(uint i = 0; i < SAMPLE_COUNT; ++i)
	{
		// Compute disk
		vec3 randFactors = readRandom(in_uv, float(i));
		vec2 dir = normalize(randFactors.xy * 2.0 - 1.0);
		float radius = projRadius * (randFactors.z * 0.85 + 0.15);
		vec2 finalDiskPoint = ndc + dir * radius;

		// Compute factor
		vec3 s = readPosition(NDC_TO_UV(finalDiskPoint));
		vec3 u = s - origin;
		ssao += max(dot(normal, u) + BIAS, EPSILON) / max(dot(u, u), EPSILON);
	}

	ssao *= (1.0 / float(SAMPLE_COUNT));
	ssao = 1.0 - ssao * STRENGTH;
	out_color = ssao;
}
			]]></source>
		</shader>
	</shaders>
</shaderProgram>

